plugins {
	id 'java'
	id 'application'
	id "com.jfrog.bintray" version "1.8.4"
	id 'maven'
	id 'maven-publish'
	id 'idea'
	id "org.javamodularity.moduleplugin" version "1.3.0"
}

def projectGroup = 'me.joshlarson'
def projectName = 'jlcommon'
def projectVersion = '1.8.4'
def projectModule = 'me.joshlarson.jlcommon'

group = projectGroup
version = projectVersion
sourceCompatibility = 11
targetCompatibility = 11

repositories {
	jcenter()
}

dependencies {
	compile group: 'org.jetbrains', name: 'annotations', version: '16.0.3'
	
	testCompile 'junit:junit:4.12'
}

jar {
	baseName = projectName
	classifier = null
	version = projectVersion
	from sourceSets.main.allSource
}

javadoc {
	inputs.property("moduleName", projectModule)
	doFirst {
		options.addStringOption('-module-path', classpath.asPath)
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	baseName = projectName
	classifier = "sources"
	version = projectVersion
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	baseName = projectName
	classifier = "javadoc"
	version = projectVersion
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

// Create the publication with the pom configuration:
publishing {
	publications {
		jlcommon(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			groupId = projectGroup
			artifactId = projectName
			version projectVersion
			pom {
				name = 'jlcommon'
				description = 'A Common Java Library'
				url = 'https://github.com/Josh-Larson/jlcommon'
				licenses {
					license {
						name = 'The MIT License'
						url = 'https://opensource.org/licenses/MIT'
					}
				}
				developers {
					developer {
						id = 'Josh-Larson'
						name = 'Josh Larson'
						email = 'joshua-github@ourclan.net'
					}
				}
				scm {
					url = 'https://github.com/Josh-Larson/jlcommon'
				}
			}
		}
	}
}

bintray {
	user ((String) (project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')))
	key ((String) (project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')))
	publications 'jlcommon'
	pkg {
		repo = 'jlcommon'
		name = projectName
		licenses = ['MIT']
		vcsUrl = 'https://github.com/Josh-Larson/jlcommon'
		version {
			name = projectVersion
			desc = 'Common Java Library'
			released = new Date()
			vcsTag = 'v' + projectVersion
		}
	}
}
